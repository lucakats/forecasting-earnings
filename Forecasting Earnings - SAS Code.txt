 * -----  Part 1  ----- *;
 
DATA EARNINGS1; SET ASS1.earnings_project1;
FORMAT FIRST 1.;
FIRST = SUBSTR(GVKEY, 1, 1);
IF FIRST LE 4;
DROP FIRST;
NOA = SUM(DLTT, DLC, CEQ, PSTK, MIB, -CHE, -IVST);
EPS = NI/CSHO;
RNOA = OIADP/NOA;
PM = OIADP/SALE;
ATO = SALE/NOA;
TACC = NI-OANCF;
INV = INVT/NOA;
AR = RECT/NOA;
ETR = TXT/PI;
SGX = XSGA/SALE;
LABEL
EPS = "Earnings per Share"
RNOA = "Return on Net Operating Assets"
PM = "Profit Margin"
ATO = "Asset Turnover"
TACC = "Total Accruals"
INV = "Inventory Ratio"
AR = "Receivables Ratio"
ETR = "Effective Tax Rate"
SGX = "Gen Exp to Sales Ratio"
;
ARRAY V(9) EPS--SGX;
DO I = 1 TO 9;
IF V(I) = . THEN DELETE;
END;
RUN;

PROC SORT DATA = EARNINGS1; BY FYEAR; RUN;

PROC MEANS DATA = EARNINGS1 NOPRINT; BY FYEAR;
VAR EPS--SGX;
OUTPUT OUT = WINS (DROP=_TYPE_ _FREQ_) P1 = L1-L9 P99 = H1-H9;
RUN;

DATA ASS1.EARNINGS; MERGE EARNINGS1 WINS; BY FYEAR;
ARRAY V(*) EPS--SGX;
ARRAY H(*) H1-H9;
ARRAY L(*) L1-L9;
DO I = 1 TO 9;
IF V(I) < L(I) THEN V(I) = L(I);
IF V(I) > H(I) THEN V(I) = H(I);
END;
KEEP GVKEY FYEAR EPS--SGX;
RUN;

PROC SORT DATA = ASS1.EARNINGS; BY GVKEY FYEAR; RUN;

* -----  Part 2  ----- *;

PROC CONTENTS DATA = ASS1.EARNINGS; RUN;

PROC FREQ DATA = ASS1.EARNINGS ORDER = FREQ NOPRINT;
TABLES GVKEY / OUT = GVKEYFREQ;
RUN;

PROC FREQ DATA = GVKEYFREQ ORDER = FREQ;
TABLES COUNT / MAXLEVELS = 6;
RUN;

PROC FREQ DATA = ASS1.EARNINGS;
TABLES FYEAR / PLOTS = FREQPLOT;
RUN;

PROC MEANS DATA = ASS1.EARNINGS MEAN MEDIAN STDDEV MIN MAX SKEW MAXDEC = 3;
VAR EPS--SGX;
RUN;

PROC UNIVARIATE DATA = ASS1.EARNINGS PLOTS;
VAR INV;
RUN;

* -----  Part 3  ----- *;

DATA ASS1.MODELS; SET ASS1.EARNINGS;
GVKEY1 = LAG(GVKEY); GVKEY2 = LAG2(GVKEY); GVKEY3 = LAG3(GVKEY); GVKEY5 = LAG5(GVKEY);
FYEAR1 = LAG(FYEAR); FYEAR2 = LAG2(FYEAR); FYEAR3 = LAG3(FYEAR); FYEAR5 = LAG5(FYEAR);
EPS1 = LAG(EPS); EPS2 = LAG2(EPS); EPS3 = LAG3(EPS); EPS5 = LAG5(EPS);
IF GVKEY = GVKEY1 AND FYEAR = FYEAR1+1 THEN DO;
	RW1 = EPS1; ERROR_RW1 = ABS((EPS-EPS1)/EPS);
END;
IF GVKEY = GVKEY1 = GVKEY2 AND FYEAR = FYEAR1+1 = FYEAR2+2  THEN DO;
	RW2 = EPS2; ERROR_RW2 = ABS((EPS-EPS2)/EPS);
	MV2 = (EPS1+EPS2)/2; ERROR_MV2 = ABS((EPS-MV2)/EPS);
END;
IF GVKEY = GVKEY1 = GVKEY2 = GVKEY3 AND
FYEAR = FYEAR1+1 = FYEAR2+2 = FYEAR3+3 THEN DO;
	RW3 = EPS3; ERROR_RW3 = ABS((EPS-EPS3)/EPS);
	MV3 = SUM(OF EPS1--EPS3)/3; ERROR_MV3 = ABS((EPS-MV3)/EPS);
END;
IF GVKEY = GVKEY1 = GVKEY2 = GVKEY3 = GVKEY5 AND
FYEAR = FYEAR1+1 = FYEAR2+2 = FYEAR3+3 = FYEAR5+5 THEN DO;
	MV5 = SUM(OF EPS1--EPS5)/2; ERROR_MV5 = ABS((EPS-MV5)/EPS);
END;
RUN;

DATA ASS1.EPSERROR; SET ASS1.MODELS (KEEP = ERROR:); RUN;

PROC MEANS DATA = ASS1.EPSERROR MEAN Q1 MEDIAN Q3 P90 P95 P99 MAX MAXDEC = 3;
OUTPUT OUT = EPS1 (DROP = _TYPE_ _FREQ_) MEAN = / AUTONAME; RUN;

PROC TRANSPOSE DATA = EPS1 OUT = EPS1; RUN;

DATA EPS2; SET EPS1;
_NAME_ = SUBSTR(_NAME_, 7, 3);
RENAME _NAME_ = MODEL COL1 = ERROR_MEAN;
RUN;

PROC SORT DATA = EPS2; BY ERROR_MEAN; RUN;

PROC SGPLOT DATA = EPS2;
VBARBASIC MODEL / RESPONSE = ERROR_MEAN DATALABEL BARWIDTH = 0.6;
XAXIS LABEL = "Model";
YAXIS LABEL = "Average Error"; 
TITLE 'Average Forecast Errors';
RUN;

* -----  Part 4  ----- *;

DATA NEARNINGS; SET ASS1.EARNINGS;
N = _N_;
RUN;

DATA FWD; SET ASS1.EARNINGS (KEEP = GVKEY FYEAR RNOA FIRSTOBS = 2);
N = _N_;
RENAME GVKEY = GVKEY1 FYEAR = FYEAR1 RNOA = RNOA1;
RUN;

DATA REGMODEL; MERGE NEARNINGS FWD; BY N;
IF GVKEY = GVKEY1 AND FYEAR+1 = FYEAR1 THEN OUTPUT;
RUN;

PROC REG DATA = REGMODEL;
MODEL RNOA1 = RNOA--SGX;
RUN;

PROC REG DATA = REGMODEL;
MODEL RNOA1 = RNOA--SGX / SELECTION = STEPWISE;
RUN;

* -----  Part 5  ----- *;

%MACRO BACKTEST(N);
	%DO I = 1 %TO &N.;

		DATA REGEST REGTEST; SET REGMODEL;
		RANDOM = RANUNI(0);
		IF RANDOM LE 0.4 THEN OUTPUT REGEST; ELSE OUTPUT REGTEST;
		RUN;
		
		PROC REG DATA = REGEST NOPRINT OUTEST = COEFF;
		MODEL RNOA1 = RNOA--SGX / SELECTION = STEPWISE;
		RUN;
		
		DATA COEFF; SET COEFF;
		RENAME
			RNOA = CO_RNOA PM = CO_PM ATO = CO_ATO TACC = CO_TACC
			INV = CO_INV AR = CO_AR ETR = CO_ETR SGX = CO_SGX;
		DROP _MODEL_ _TYPE_ _DEPVAR_ _RMSE_ RNOA1;
		RUN;
		
		DATA EST; SET REGTEST;
		IF _N_ = 1 THEN SET COEFF;
		P_RNOA1 = SUM(INTERCEPT, CO_RNOA*RNOA, CO_PM*PM, CO_ATO*ATO, CO_TACC*TACC,
						CO_INV*INV, CO_AR*AR, CO_ETR*ETR, CO_SGX*SGX);
		ERROR = ABS(RNOA1 - P_RNOA1);
		RUN;
		
		PROC MEANS DATA = EST NOPRINT;
		VAR ERROR;
		OUTPUT OUT = MEAN MEAN = MEAN MIN = MIN Q1 = Q1 MEDIAN = MEDIAN Q3 = Q3
					P90 = P90 P95 = P95 P99 = P99 MAX = MAX;
		RUN;
		
		DATA MEAN; SET MEAN;
		EST = &I.;
		KEEP MEAN MIN Q1 MEDIAN Q3 P90 P95 P99 MAX EST;
		RUN;
		
		%IF &I. EQ 1 %THEN %DO;
			DATA ERRORSTATS; SET MEAN; RUN;
		%END;
		%ELSE %DO;
			PROC APPEND BASE = ERRORSTATS DATA = MEAN; RUN;
		%END;
	%END;
%MEND BACKTEST;

%BACKTEST(100);

PROC MEANS DATA = ERRORSTATS MAXDEC = 3 MIN MEAN MAX;
VAR MEAN--MAX;
RUN;

PROC MEANS DATA = REGMODEL MEDIAN MEAN;
VAR RNOA1;
RUN;
